
plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "1.3.41"
}
ext{
    kotlin_version ="1.3.41"
    mc_version = '1.12-R0.1-SNAPSHOT'
    bc_version ='1.12-SNAPSHOT'
}

allprojects{
    group = 'cf.wayzer'
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url = "https://hub.spigotmc.org/nexus/content/groups/public/"
        }
        maven {
            url  "https://dl.bintray.com/way-zer/maven"
        }
    }
}
shadowJar {
//    minimize()
    dependencies{
        include(dependency(':LibraryManager:'))
    }
    exclude "example/*"
}


//start

dependencies{
    compile 'cf.wayzer:LibraryManager:1.1'
    compile "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-scripting-jvm-host:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compileOnly "org.spigotmc:spigot-api:$mc_version"
    compileOnly "net.md-5:bungeecord-chat:$bc_version"
    compileOnly files("$rootDir/lib/PowerNBT.jar")
//    compile "me.dpohvar.powernbt:PowerNBT:0.8.9.2"
}
version="1.3.4"
processResources{
    filesMatching(["plugin.yml","bungee.yml"]){
        filter{
            it.replace("@version@", project.version.toString())
        }
    }
}
def items = subprojects[0] as Project
task buildArtifact(type: Zip){
    dependsOn items.getTasksByName("classes",false)
    dependsOn shadowJar
    from(file(items.buildDir.path+"/classes/kotlin/")){
        includeEmptyDirs = false
        include "**/*.class"
        into("items")
    }
    from(jar.getArchiveFile()){
        rename(".jar","-lib.jar")
    }
    from(shadowJar.getArchiveFile()){
        rename("-all","")
    }
    archiveFileName ="SuperItem-${project.version}.zip"
    destinationDirectory =file("$rootDir/artifacts/")
}
artifacts {
    buildArtifact
}
